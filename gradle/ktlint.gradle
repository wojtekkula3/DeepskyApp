configurations {
    ktlint
}
dependencies {
    ktlint("com.pinterest.ktlint:ktlint-cli:1.3.0") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.SHADOWED))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    inputs.files(fileTree("src").matching {
        include "**/*.kt"
        exclude "test/**/*.kt", "androidTest/**/*.kt"
    })
    outputs.file("build/reports/ktlint-report.xml")
    outputs.cacheIf { true }

    args "src/**/*.kt", "!src/test/**/*.kt", "!src/androidTest/**/*.kt",
            "--editorconfig=${rootDir}/gradle/.editorconfig",
            "--reporter=html,output=$rootDir/build/reports/ktlint/ktlint-report.html",
            "--reporter=plain"

    maxHeapSize = "256m"
}
